<!DOCTYPE html>
<html lang="en">
    <head>
        <title>Shadertoy-Style Scene</title>
        <meta charset="utf-8">
        <style>
            body {
                margin: 0px;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
        <div id="container"></div>
        <script src="js/three.min.js"></script>

        <script id="vs" type="x-shader/x-vertex">
            attribute vec3 position;
            void main() {
                gl_Position = vec4(position, 1.0);
            }
        </script>

        <script id="fs" type="x-shader/x-fragment">
            precision mediump float;
            uniform float time;
            uniform vec2 res;
            uniform sampler2D hotdog, grill;

            /**
             * Part 2 Challenges
             * - Change the diffuse color of the sphere to be blue
             * - Change the specual color of the sphere to be green
             * - Make one of the lights pulse by having its intensity vary over time
             * - Add a third light to the scene
             */

            const int MAX_MARCHING_STEPS = 255;
            const float MIN_DIST = 0.0;
            const float MAX_DIST = 100.0;
            const float EPSILON = 0.0001;

            /**
             * Signed distance function for a sphere centered at the origin with radius r;
             */
            float sphereSDF(vec3 p, float r) {
                return length(p) - r;
            }

            float rRectSDF(vec3 p, vec3 t, float r) {
                return length(max(abs(p) - t, 0.0)) - r;
            }

            float torusSDF(vec3 p, vec2 t) {
                vec2 q = vec2(length(p.xz) - t.x, p.y);
                return length(q) - t.y;
            }

            float capsuleSDF(vec3 p, vec3 a, vec3 b, float r) {
                vec3 pa = p - a;
                vec3 ba = b - a;
                float h = clamp(dot(pa, ba)/dot(ba, ba), 0.0, 1.0);
                return length(pa - ba*h) - r;
            }

            vec3 opTwist(vec3 p) {
                float c = cos(sin(time) * 20.0 * p.y+10.0);
                float s = sin(sin(time) * 20.0 * p.y+10.0);
                mat2 m = mat2(c,-s,s,c);
                return vec3(m*p.xz,p.y);
            }

            // vec3 opTx(vec3 p, mat4 m) {
            //     return invert(m) * p;
            // }

            float smin(float a, float b, float k) {
                float h = clamp(.5 + .5*(b-a) / k, 0.0, 1.0);
                return mix(b, a, h) - k*h*(1.0-h);
            }

            /**
             * Signed distance function describing the scene.
             * 
             * Absolute value of the return value indicates the distance to the surface.
             * Sign indicates whether the point is inside or outside the surface,
             * negative indicating inside.
             */
            float sceneSDF(vec3 p) {
                float sphere = sphereSDF(p - vec3(1.5, 0.0, 0.0), 0.5);
                float box = rRectSDF(p - vec3(2.4, 0.0, 0.0), vec3(0.25), .15);
                float torus1 = torusSDF(opTwist(p - vec3(-1.5, 0.0, 0.0)), vec2(.2));
                float torus2 = torusSDF(p - vec3(1.5, 0.0, 0.0), vec2(.5));

                float hotdog = capsuleSDF(p, vec3(0.5, -1.0, 0.0), vec3(-0.5, 1.0, 0.0), .15);
                //float bun = capsuleSDF(p, vec3(0.0, -1.0, 0.0), vec3(0.0, 1.0, 0.0), .65);

                float displacement = sin(sin(time)*5.0 * p.x) * sin(sin(time)*5.0 * p.y) * sin(sin(time)*5.0 * p.z);
                float u = smin(sphere, box, 0.05);
                u = max(u, (torus2 + displacement));
                u = min(u, torus1);
                u = min(u, hotdog);
                //u = min(u, bun);
                return u;
            }

            /**
             * Return the shortest distance from the eyepoint to the scene surface along
             * the marching direction. If no part of the surface is found between start and end,
             * return end.
             * 
             * eye: the eye point, acting as the origin of the ray
             * marchingDirection: the normalized direction to march in
             * start: the starting distance away from the eye
             * end: the max distance away from the ey to march before giving up
             */
            float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
                float depth = start;
                for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
                    float dist = sceneSDF(eye + depth * marchingDirection);
                    if (dist < EPSILON) {
                        return depth;
                    }
                    depth += dist;
                    if (depth >= end) {
                        return end;
                    }
                }
                return end;
            }
                        

            /**
             * Return the normalized direction to march in from the eye point for a single pixel.
             * 
             * fieldOfView: vertical field of view in degrees
             * size: resolution of the output image
             * fragCoord: the x,y coordinate of the pixel in the output image
             */
            vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
                vec2 xy = fragCoord - size / 2.0;
                float z = size.y / tan(radians(fieldOfView) / 2.0);
                return normalize(vec3(xy, -z));
            }

            /**
             * Using the gradient of the SDF, estimate the normal on the surface at point p.
             */
            vec3 estimateNormal(vec3 p) {
                return normalize(vec3(
                    sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
                    sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
                    sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
                ));
            }

            /**
             * Lighting contribution of a single point light source via Phong illumination.
             * 
             * The vec3 returned is the RGB color of the light's contribution.
             *
             * k_a: Ambient color
             * k_d: Diffuse color
             * k_s: Specular color
             * alpha: Shininess coefficient
             * p: position of point being lit
             * eye: the position of the camera
             * lightPos: the position of the light
             * lightIntensity: color/intensity of the light
             *
             * See https://en.wikipedia.org/wiki/Phong_reflection_model#Description
             */
            vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                                      vec3 lightPos, vec3 lightIntensity) {
                vec3 N = estimateNormal(p);
                vec3 L = normalize(lightPos - p);
                vec3 V = normalize(eye - p);
                vec3 R = normalize(reflect(-L, N));
                
                float dotLN = dot(L, N);
                float dotRV = dot(R, V);
                
                if (dotLN < 0.0) {
                    // Light not visible from this point on the surface
                    return vec3(0.0, 0.0, 0.0);
                } 
                
                if (dotRV < 0.0) {
                    // Light reflection in opposite direction as viewer, apply only diffuse
                    // component
                    return lightIntensity * (k_d * dotLN);
                }
                return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
            }

            /**
             * Lighting via Phong illumination.
             * 
             * The vec3 returned is the RGB color of that point after lighting is applied.
             * k_a: Ambient color
             * k_d: Diffuse color
             * k_s: Specular color
             * alpha: Shininess coefficient
             * p: position of point being lit
             * eye: the position of the camera
             *
             * See https://en.wikipedia.org/wiki/Phong_reflection_model#Description
             */
            vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
                const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
                vec3 color = ambientLight * k_a;
                
                vec3 light1Pos = vec3(4.0 * sin(time),
                                      2.0,
                                      4.0 * cos(time));
                vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
                
                color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                              light1Pos,
                                              light1Intensity);
                
                vec3 light2Pos = vec3(2.0 * sin(0.37 * time),
                                      2.0 * cos(0.37 * time),
                                      2.0);
                vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
                
                color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                              light2Pos,
                                              light2Intensity); 

                vec3 light3Pos = vec3(1.0 * sin(time), 0.0, 1.0);
                vec3 light3Intensity = vec3(.4);
                color += phongContribForLight(vec3(.7, .6, .84) * sin(time), vec3(.4, .2, .1), 5.0, p, eye, light3Pos, light3Intensity);   
                return color;
            }


            void main()
            {
                vec2 uv = vec2(gl_FragCoord.x / res.x, gl_FragCoord.y / res.y);
                vec3 dir = rayDirection(45.0, res, gl_FragCoord.xy);
                vec3 eye = vec3(1.0, 0.0, 10.0);
                float dist = shortestDistanceToSurface(eye, dir, MIN_DIST, MAX_DIST);

                // The closest point on the surface to the eyepoint along the view ray
                vec3 p = eye + dist * dir;

                vec4 grillTex = texture2D(grill, uv);
                
                if (dist > MAX_DIST - EPSILON) {
                    // Didn't hit anything
                    gl_FragColor = grillTex;
                    return;
                }
                
                vec3 K_a = vec3(0.2, 0.2, 0.2);
                vec3 K_d = vec3(0.7, 0.1, 0.4);
                vec3 K_s = vec3(1.0, 1.0, 1.0);
                float shininess = 10.0;
                
                vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
                
                vec4 tex1 = texture2D(hotdog, uv);
                
                if (p.x > -0.8 && p.x < 0.8) {
                    gl_FragColor = vec4(mix(color, tex1.rgb, 0.7), 1.0);
                } else {
                    gl_FragColor = vec4(color, 1.0);
                }
            }

        </script>


        <script>
            var scene, camera, renderer;
            var material;
            var fullScreenQuad;
            var vs = document.getElementById('vs').textContent;
            var fs = document.getElementById('fs').textContent;

            init();
            render();

            function init() {
                scene = new THREE.Scene();
                let width = window.innerWidth;
                let height = window.innerHeight;

                camera = new THREE.OrthographicCamera(width/-2, width/2, height/2, height/-2, 0.1, 1000);
                camera.position.z = .5;

                renderer = new THREE.WebGLRenderer();
                renderer.setSize(window.innerWidth, window.innerHeight);
                document.body.appendChild(renderer.domElement);

                var res = new THREE.Vector2(window.innerWidth, window.innerHeight);
                var loader = new THREE.TextureLoader();

                var tHotdog = loader.load('hotdog.png', (tex) => {
                    tex.wrapS = tex.wrapT = THREE.RepeatWrapping;
                });

                var tGrill = loader.load('grill.jpg');

                var uniforms = {
                    time: {type: "f", value: .5},
                    res: {type: "v2v", value: res},
                    hotdog: {type: "t", value: tHotdog},
                    grill: {type: "t", value: tGrill},
                };

                var plane = new THREE.PlaneGeometry(window.innerWidth, window.innerHeight);
                material = new THREE.RawShaderMaterial({
                    uniforms: uniforms,
                    vertexShader: vs,
                    fragmentShader: fs
                });

                fullScreenQuad = new THREE.Mesh(plane, material);
                fullScreenQuad.material.side = THREE.DoubleSide;
                scene.add(fullScreenQuad);

                window.addEventListener('resize', (e) => {
                    camera.aspect = window.innerWidth / window.innerHeight;
                    camera.updateProjectionMatrix();
                    renderer.setSize(window.innerWidth, window.innerHeight);
                    material.uniforms.res.value = new THREE.Vector2(window.innerWidth, window.innerHeight);
                }, false);
            }

            function render() {
                requestAnimationFrame(render);
                material.uniforms.time.value = performance.now() * .0005;
                renderer.render(scene, camera);
            }
        </script>
    </body>
</html>